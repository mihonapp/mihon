import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext {
    // Git is needed in your system PATH for these commands to work.
    // If it's not installed, you can return a random value as a workaround
    getCommitCount = {
        return 'git rev-list --count HEAD'.execute().text.trim()
        // return "1"
    }

    getGitSha = {
        return 'git rev-parse --short HEAD'.execute().text.trim()
        // return "1"
    }

    getBuildTime = {
        def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
        df.setTimeZone(TimeZone.getTimeZone("UTC"))
        return df.format(new Date())
    }
}

def includeUpdater() {
    return hasProperty("include_updater")
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    publishNonDefault true

    defaultConfig {
        applicationId "eu.kanade.tachiyomi"
        minSdkVersion 16
        targetSdkVersion 24
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionCode 10
        versionName "0.2.3"

        buildConfigField "String", "COMMIT_COUNT", "\"${getCommitCount()}\""
        buildConfigField "String", "COMMIT_SHA", "\"${getGitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${getBuildTime()}\""
        buildConfigField "boolean", "INCLUDE_UPDATER", "${includeUpdater()}"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            versionNameSuffix "-${getCommitCount()}"
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

}

dependencies {

    // Modified dependencies
    compile 'com.github.inorichi:subsampling-scale-image-view:421fb81'
    compile 'com.github.inorichi:ReactiveNetwork:69092ed'

    // Android support library
    final support_library_version = '24.1.1'
    compile "com.android.support:support-v4:$support_library_version"
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:cardview-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:support-annotations:$support_library_version"
    compile "com.android.support:customtabs:$support_library_version"

    // ReactiveX
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

    // Network client
    compile "com.squareup.okhttp3:okhttp:3.4.1"

    // REST
    final retrofit_version = '2.1.0'
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"

    // IO
    compile 'com.squareup.okio:okio:1.9.0'

    // JSON
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.github.salomonbrys.kotson:kotson:2.3.0'

    // YAML
    compile 'com.github.bmoliveira:snake-yaml:v1.18-android'

    // JavaScript engine
    compile 'com.squareup.duktape:duktape-android:0.9.5'

    // Disk cache
    compile 'com.jakewharton:disklrucache:2.0.2'

    // Parse HTML
    compile 'org.jsoup:jsoup:1.9.2'

    // Changelog
    compile 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'

    // Database
    compile "com.pushtorefresh.storio:sqlite:1.10.0"

    // Model View Presenter
    final nucleus_version = '3.0.0'
    compile "info.android15.nucleus:nucleus:$nucleus_version"
    compile "info.android15.nucleus:nucleus-support-v4:$nucleus_version"
    compile "info.android15.nucleus:nucleus-support-v7:$nucleus_version"

    // Dependency injection
    compile "uy.kohesive.injekt:injekt-core:1.16.1"

    // Image library
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    // Logging
    compile 'com.jakewharton.timber:timber:4.1.2'

    // Crash reports
    compile 'ch.acra:acra:4.9.0'

    // UI
    compile 'com.dmitrymalkovich.android:material-design-dimens:1.2'
    compile 'com.github.dmytrodanylyk.android-process-button:library:1.0.4'
    compile 'eu.davidea:flexible-adapter:4.2.0'
    compile 'com.nononsenseapps:filepicker:2.5.2'
    compile 'com.github.amulyakhare:TextDrawable:558677e'
    compile 'com.afollestad.material-dialogs:core:0.8.6.2'
    compile 'net.xpece.android:support-preference:0.8.1'
    compile 'me.zhanghai.android.systemuihelper:library:1.0.0'
    compile 'org.adw.library:discrete-seekbar:1.0.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.1.2'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

buildscript {
    ext.kotlin_version = '1.0.3'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
}
