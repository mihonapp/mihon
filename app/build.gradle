import java.text.SimpleDateFormat

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

retrolambda {
    jvmArgs '-noverify'
}

ext {
    // Git is needed in your system PATH for these commands to work.
    // If it's not installed, you can return a random value as a workaround
    getCommitCount = {
        return 'git rev-list --count origin/master'.execute().text.trim()
        // return "1"
    }

    getGitSha = {
        return 'git rev-parse --short HEAD'.execute().text.trim()
        // return "1"
    }

    getBuildTime = {
        def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
        df.setTimeZone(TimeZone.getTimeZone("UTC"))
        return df.format(new Date())
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    publishNonDefault true

    defaultConfig {
        applicationId "eu.kanade.mangafeed"
        minSdkVersion 16
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "COMMIT_COUNT", "\"${getCommitCount()}\""
        buildConfigField "String", "COMMIT_SHA", "\"${getGitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${getBuildTime()}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '23.1.1'
    final DAGGER_VERSION = '2.0.2'
    final HAMCREST_VERSION = '1.3'
    final MOCKITO_VERSION = '1.10.19'
    final STORIO_VERSION = '1.7.0'
    final ICEPICK_VERSION = '3.1.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(":SubsamplingScaleImageView")

    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.7.0'
    compile 'com.squareup.okhttp:okhttp:2.7.0'
    compile 'com.squareup.okio:okio:1.6.0'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.1'
    compile "com.pushtorefresh.storio:sqlite:$STORIO_VERSION"
    compile "com.pushtorefresh.storio:sqlite-annotations:$STORIO_VERSION"
    compile 'info.android15.nucleus:nucleus:2.0.4'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'uk.co.ribot:easyadapter:1.5.0@aar'
    compile 'ch.acra:acra:4.7.0'
    compile "frankiesardo:icepick:$ICEPICK_VERSION"
    provided "frankiesardo:icepick-processor:$ICEPICK_VERSION"
    compile 'com.github.dmytrodanylyk.android-process-button:library:1.0.4'
    compile 'eu.davidea:flexible-adapter:4.2.0@aar'
    compile 'com.nononsenseapps:filepicker:2.5.0'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    apt "com.pushtorefresh.storio:sqlite-annotations-processor:$STORIO_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile('com.mikepenz:materialdrawer:4.6.1@aar') {
        transitive = true
    }
    compile('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }

    testCompile 'junit:junit:4.12'
    testCompile "org.hamcrest:hamcrest-core:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-library:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-integration:$HAMCREST_VERSION"
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    androidTestApt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
}
