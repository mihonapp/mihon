import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext {
    // Git is needed in your system PATH for these commands to work.
    // If it's not installed, you can return a random value as a workaround
    getCommitCount = {
        return 'git rev-list --count origin/master'.execute().text.trim()
        // return "1"
    }

    getGitSha = {
        return 'git rev-parse --short HEAD'.execute().text.trim()
        // return "1"
    }

    getBuildTime = {
        def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
        df.setTimeZone(TimeZone.getTimeZone("UTC"))
        return df.format(new Date())
    }
}

def includeUpdater() {
    return hasProperty("include_updater")
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    publishNonDefault true

    defaultConfig {
        applicationId "eu.kanade.tachiyomi"
        minSdkVersion 16
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        versionCode 6
        versionName "0.2.0"

        buildConfigField "String", "COMMIT_COUNT", "\"${getCommitCount()}\""
        buildConfigField "String", "COMMIT_SHA", "\"${getGitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${getBuildTime()}\""
        buildConfigField "boolean", "INCLUDE_UPDATER", "${includeUpdater()}"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            versionNameSuffix ".${getCommitCount()}"
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    // http://stackoverflow.com/questions/32759529/androidhttpclient-not-found-when-running-robolectric
    useLibrary 'org.apache.http.legacy'

}

kapt {
    generateStubs = true
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '23.3.0'
    final DAGGER_VERSION = '2.2'
    final OKHTTP_VERSION = '3.2.0'
    final RETROFIT_VERSION = '2.0.1'
    final STORIO_VERSION = '1.8.0'
    final MOCKITO_VERSION = '1.10.19'

    // Modified dependencies
    compile 'com.github.inorichi:subsampling-scale-image-view:421fb81'
    compile 'com.github.inorichi:ReactiveNetwork:69092ed'

    // Android support library
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:percent:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:preference-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:preference-v14:$SUPPORT_LIBRARY_VERSION"

    // ReactiveX
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.1'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.1'

    // Network client
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:okhttp-urlconnection:$OKHTTP_VERSION"

    // REST
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"

    // IO
    compile 'com.squareup.okio:okio:1.7.0'

    // JSON
    compile 'com.google.code.gson:gson:2.6.2'

    // Disk cache
    compile 'com.jakewharton:disklrucache:2.0.2'

    // Parse HTML
    compile 'org.jsoup:jsoup:1.8.3'

    // Database
    compile "com.pushtorefresh.storio:sqlite:$STORIO_VERSION"
    compile "com.pushtorefresh.storio:sqlite-annotations:$STORIO_VERSION"
    kapt "com.pushtorefresh.storio:sqlite-annotations-processor:$STORIO_VERSION"

    // Model View Presenter
    compile 'info.android15.nucleus:nucleus:2.0.5'

    // Dependency injection
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Image library
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Logging
    compile 'com.jakewharton.timber:timber:4.1.2'

    // Crash reports
    compile 'ch.acra:acra:4.8.5'

    // UI
    compile 'com.github.dmytrodanylyk.android-process-button:library:1.0.4'
    compile 'eu.davidea:flexible-adapter:4.2.0'
    compile 'com.nononsenseapps:filepicker:2.5.2'
    compile 'com.github.amulyakhare:TextDrawable:558677e'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.5@aar') {
        transitive = true
    }

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.1'
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    kaptTest "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

buildscript {
    ext.kotlin_version = '1.0.1'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
}
